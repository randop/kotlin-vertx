plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.70'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

group = 'com.randolphledesma'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
  jcenter()
}

ext {
  kotlinVersion = '1.3.70'
  kotlinCoroutinesVersion = '1.3.5'
  vertxVersion = '3.9.0'
  junitJupiterEngineVersion = '5.4.0'
  daggerVersion = '2.27'
}

mainClassName = 'com.randolphledesma.gad.AppKt'

dependencies {
  implementation "io.vertx:vertx-web:$vertxVersion"  
  implementation "io.vertx:vertx-web-client:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin-coroutines:$vertxVersion"
  implementation "io.vertx:vertx-mysql-client:$vertxVersion"
  implementation "io.vertx:vertx-config:$vertxVersion"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

  implementation "com.google.dagger:dagger:$daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
  kapt "com.google.dagger:dagger-compiler:$daggerVersion"
  kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}

compileKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

idea {
    module {
        testSourceDirs += file('build/generated/source/kapt/test')
        sourceDirs += file('build/generated/source/kapt/main')
    }
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes "Main-Class": mainClassName
    }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

// Based on the guide here: https://github.com/codecov/example-gradle
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'run'